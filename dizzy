#!/usr/bin/env perl
use strict;
use warnings;
use 5.010;

use lib 'lib';
use OpenGL qw(:all);
use Math::Trig;
use Time::HiRes qw(sleep time);
use Dizzy::Handlers;
use Dizzy::Core;
use Dizzy::GLFeatures;

# handler functions
sub handler_idle {
	handler_render();

	glFlush();
	glutSwapBuffers();
}

sub handler_keyboard {
	my ($key, $x, $y) = @_;

	Dizzy::Handlers::invoke("keyboard",
		mouse_x => $x,
		mouse_y => $y,
		ascii   => chr($key),
	);
}

sub handler_keyboardspecial {
	my ($key, $x, $y) = @_;

	my %keys = (
		GLUT_KEY_LEFT() => "LEFT",
		GLUT_KEY_RIGHT() => "RIGHT",
	);

	Dizzy::Handlers::invoke("keyboard",
		mouse_x => $x,
		mouse_y => $y,
		special => $keys{$key} // "OTHER:$key",
	);
}

sub handler_render {
	Dizzy::Handlers::invoke("render");
}

my %options = Dizzy::Core::init_arguments();

# initialize OpenGL
glutInit();
glutInitDisplayMode(GLUT_DOUBLE | GLUT_RGBA);
if ($options{fullscreen}) {
	foreach my $mode (
		glutGet(GLUT_SCREEN_WIDTH) . "x" . glutGet(GLUT_SCREEN_HEIGHT),
		"1024x768",
		"800x600",
		""
	) {
		glutGameModeString($mode);
		if (glutGameModeGet(GLUT_GAME_MODE_POSSIBLE)) {
			glutEnterGameMode();
			last;
		}
	}
	if (glutGameModeGet(GLUT_GAME_MODE_ACTIVE) == 0) {
		print "Fatal error: Couldn't initialize any game mode. Try without the -f option.\n";
		exit(1);
	}
} else {
	glutInitWindowSize($options{width} || 800, $options{height} || 500);
	glutCreateWindow("Dizzy");
}

glutIdleFunc      (\&handler_idle);
glutDisplayFunc   (\&handler_render);
glutKeyboardFunc  (\&handler_keyboard);
glutSpecialFunc   (\&handler_keyboardspecial);

Dizzy::Handlers::register_last(
	render => sub {
		Dizzy::Core::_fps_tick();
		Dizzy::Handlers::GO_ON;
	},
);

Dizzy::GLFeatures::update_capabilities();

# initialize dizzy subsystems
Dizzy::Core::init_subsystems(%options);

if ($options{debug_time_startup}) {
	print "debug: startup complete, exiting as requested\n";
	exit(0);
}

glutMainLoop();

__END__

=head1 NAME

B<dizzy> - a graphics demo that makes you dizzy using rotating textures

=head1 SYNOPSIS

B<dizzy> [B<-f>|B<-w> I<width> B<-h> I<height>] [B<-t> I<switch_module>] [B<-T> I<options>]

=head1 DESCRIPTION

B<dizzy> is a graphics demo that rotates planes of patterns on a colored
background to make you dizzy. Textures can be cross-faded and there is a mode
that automatically changes textures, allowing Dizzy to be run as a screensaver.

=head1 OPTIONS

=over

=item B<-w> I<width>

=item B<--width> I<width>

=item B<-h> I<height>

=item B<--height> I<height>

Sets the window width and height.

=item B<-f>

=item B<--fullscreen>

Attempts to switch into a true fullscreen mode, if possible. The window size
parameters are ignored.

=item B<-a>

=item B<--automode> I<time>

Automatically switches textures after a specified number of seconds has passed.
I<time> can be fractional and the decimal separator is always the period.

=item B<-t> I<module>

=item B<--texswitcher> I<module>

Selects the texture switching module to use. Default is B<Simple>.

See below for available texture switchers and their descriptions.

=item B<-T> I<option>=I<value>

=item B<--texswitcher-options> I<option>=I<value>

Passes an option I<option> with the value I<value> to the selected texture
switcher. The available options depend on the texture switcher used.

This option can be given multiple times to set multiple options.

=item B<-r> I<resolution>

=item B<--texture-resolution> I<resolution>

Changes the texture resolution. I<resolution> must be a power of two. The default
value is 256.

=item B<-R> I<resolution>

=item B<--shader-resolution> I<resolution>

Changes the resolution used when rendering using shaders. I<resolution> must be
a power of two. The default is 1024.

=item B<-z> I<zoom>

=item B<--zoom> I<zoom>

Zooms the textures. The default value is 100.

=item B<-c> I<path>

=item B<--cache-paths> I<path>

Use a different path for cached textures. This option can be specified multiple
times.

=item B<-C>

=item B<--disable-cache>

Don't use any texture cache at all.

=item B<--debug-show-planes>

Zooms out of the normal view so you can see how Dizzy creates the animation. A
white border will also be drawn around the area that would have been shown had
this option not been used.

=back

=head1 TEXTURE SWITCHERS

=head2 Simple

A simple texture switcher. It just sets the new texture when it is told to do
so. It takes no options.

=head2 Blend

A texture switcher that crossfades between textures to generate a smooth
transition. It takes one option:

=over

=item B<blend>=I<duration>

Sets the duration of a blend to I<duration> seconds. The value can be fractional
(the decimal separator is always a period).  The default value is 2.

Note that you have to add the time you specify here to the automode time, so if
you want the transition to take two seconds and every image to stay for five
seconds, you set the duration to 2 and automode to 7 (not 5).

=back

=head1 KEYBOARD COMMANDS

=over

=item Cursor left

Select previous available texture.

=item Cursor right

Select next available texture.

=item Escape

=item q

Exit Dizzy.

=back

=cut
