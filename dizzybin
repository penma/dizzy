#!/usr/bin/env perl
use strict;
use warnings;
use 5.010;

use Getopt::Long qw(:config no_ignore_case bundling);

sub usage_message {
	print STDERR << "EOH";
usage: dizzy [options]

   Graphics settings:
     -w num           set window width
     -h num           set window height
     -f               run in fullscreen mode
     -r num           set texture resolution (power of two)
     -s num           set texture display scale

   Auto mode:
     -a               activate auto mode
     -A num           set a new texture every num seconds

   Texture blending options:
     -t               activate texture blending
     -T num           duration of the transition in seconds
EOH
}

my %options = (
	help                   => sub { usage_message(); exit(0); },

	width                  => 0,
	height                 => 0,
	fullscreen             => 0,
	texture_resolution     => 64,
	texture_scale          => 50,

	automode               => 0,
	automode_wait          => 7,

	texblend               => 0,
	texblend_duration      => 2,
);

GetOptions(\%options,
	'help|?',

	'width|w=i',
	'height|h=i',
	'fullscreen|f+',
	'texture_resolution|texture-resolution|r=i',
	'texture_scale|texture-scale|s=f',

	'automode|a+',
	'automode_wait|automode-wait|A=f',

	'texblend|t+',
	'texblend_duration|texblend-duration|T=f',

	'debug_show_planes|debug-show-planes+',
) or (usage_message(), exit(1));

########
use OpenGL qw(:all);
use Math::Trig;
use Time::HiRes qw(sleep time);
use Dizzy::TextureGenerator;
use Dizzy::TextureManager;
use Dizzy::TexBlend;
use Dizzy::Render;
use Dizzy::GLUT;
use Dizzy::Handlers;

Dizzy::GLUT::init(title => "Dizzy", width => $options{width}, height => $options{height}, fullscreen => $options{fullscreen});
Dizzy::Render::init_view(texture_scale => $options{texture_scale});

Dizzy::TextureManager::init(
	texture_resolution => $options{texture_resolution},
);
sub wrapval {
	   if ($_[0] < 0.0) { return 1.0; }
	elsif ($_[0] > 1.0) { return 0.0; }
	else                { return $_[0]; }
}

# read textures dir
opendir(my $textures, "textures/");
foreach my $texture (sort readdir($textures)) {
	next if (! -f "textures/$texture");
	my $tex = do("textures/$texture");
	Dizzy::TextureManager::add(%{$tex});
}

if ($options{texblend}) {
	Dizzy::TexBlend::init(
		texblend_duration => $options{texblend_duration},
	);
} else {
	# move this to its own module too...
	Dizzy::Handlers::register(
		texture_switch => sub {
			my %args = @_;
			my $some_texture;
			print "<Simple texture switcher> Changing texture (from GL texture $args{old_gl_texture})!\n";
			print "<Simple texture switcher> using GL texture $args{gl_texture}\n";
			glBindTexture(GL_TEXTURE_2D, $args{gl_texture});
			Dizzy::Handlers::invoke("texture_switched", %args);
			Dizzy::Handlers::STOP;
		}
	);
}

Dizzy::Handlers::register(
	keyboard => sub {
		my %args = @_;

		# so we don't get warning spam
		$args{ascii} //= "";
		$args{special} //= -1;

		if ($args{ascii} eq "\e") { # escape
			exit(0);
		} elsif ($args{special} == GLUT_KEY_LEFT or $args{special} == GLUT_KEY_RIGHT) {
			Dizzy::Handlers::invoke("texture_switch",
				direction => (($args{special} == GLUT_KEY_LEFT) ? -1 : +1),
			);
		}

		Dizzy::Handlers::GO_ON;
	},

	# notify user about texture switches
	texture_changed => sub {
		my %args = @_;
		print "*** selected texture \"$args{name}\"\n";
		Dizzy::Handlers::GO_ON;
	},

	# auto mode. move? or leave it here?
	render => sub {
		state $last_switch = 0;

		if ($options{automode}) {
			if ($last_switch + $options{automode_wait} <= time()) {
				$last_switch = time();
				Dizzy::Handlers::invoke("texture_switch", direction => +1);
			}
		}

		Dizzy::Handlers::GO_ON;
	},

	# to be moved to: somewhere else (or keep it here? it's nice and warm)
	render => sub {
		Dizzy::Render::render_planes(
			tick => time() - $^T,
			rotator_func => sub {
				my ($tick, $plane) = @_;
				if ($options{debug_show_planes}) {
					glScalef(0.2, 0.2, 0.2);
				}
				if ($plane == 1) {
					glRotatef($tick * 5, 0, 0, 1);
					glTranslatef(sin($tick * 0.5), cos($tick * 0.75), 0);
				} elsif ($plane == 2) {
					glRotatef($tick * -2.5, 0, 0, 1);
					glTranslatef(sin($tick * 0.5), cos($tick * 0.75), 0);
				}
			},
		);

		Dizzy::Handlers::GO_ON;
	},
);

Dizzy::TextureManager::set(0);

Dizzy::GLUT::run();

